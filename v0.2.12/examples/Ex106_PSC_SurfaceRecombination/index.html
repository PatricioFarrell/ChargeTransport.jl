<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>PSC device with surface recombination (1D). · ChargeTransport.jl</title><meta name="title" content="PSC device with surface recombination (1D). · ChargeTransport.jl"/><meta property="og:title" content="PSC device with surface recombination (1D). · ChargeTransport.jl"/><meta property="twitter:title" content="PSC device with surface recombination (1D). · ChargeTransport.jl"/><meta name="description" content="Documentation for ChargeTransport.jl."/><meta property="og:description" content="Documentation for ChargeTransport.jl."/><meta property="twitter:description" content="Documentation for ChargeTransport.jl."/><script data-outdated-warner src="../../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.050/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.16.8/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../search_index.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../../general/">ChargeTransport.jl</a></span></div><button class="docs-search-query input is-rounded is-small is-clickable my-2 mx-auto py-1 px-2" id="documenter-search-query">Search docs (Ctrl + /)</button><ul class="docs-menu"><li><a class="tocitem" href="../../general/">ChargeTransport.jl – Simulating charge transport in semiconductors</a></li><li><a class="tocitem" href="../../backgroundinfo/">Mathematical drift-diffusion models</a></li><li><span class="tocitem">How to get started</span><ul><li><a class="tocitem" href="../../GeneralInformation/">Code overview</a></li><li><a class="tocitem" href="../../GaAs/">van Roosbroeck system</a></li><li><a class="tocitem" href="../../PSC/">Perovskite solar cell</a></li></ul></li><li><a class="tocitem" href="../../allindex/">Types, Constructors and Methods</a></li><li><span class="tocitem">Pluto Notebooks</span><ul><li><a class="tocitem" href="../../nbhtml/PSC_example/">PSC example</a></li></ul></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../Ex101_PIN/">GaAs diode (1D).</a></li><li><a class="tocitem" href="../Ex102_PIN_nodal_doping/">GaAs diode with spatially varying doping (1D).</a></li><li><a class="tocitem" href="../Ex103_PSC_IVMeasurement/">PSC device with ions and different I-V scan protocols (1D).</a></li><li><a class="tocitem" href="../Ex104_PSC_Photogeneration/">PSC device with photogeneration rate (1D).</a></li><li><a class="tocitem" href="../Ex105_PSC_gradedFlux/">Three-layer PSC device with graded interfaces &amp; Ohmic contacts (1D).</a></li><li class="is-active"><a class="tocitem" href>PSC device with surface recombination (1D).</a></li><li><a class="tocitem" href="../Ex107_MoS2_withIons_BarrierLowering/">MoS2 with moving defects and Schottky Barrier Lowering.</a></li><li><a class="tocitem" href="../Ex108_CIGS_WithTraps/">CIGS: stationary with traps and Schottky contacts.</a></li><li><a class="tocitem" href="../Ex109_Traps/">GaAs diode: transient with traps (1D).</a></li><li><a class="tocitem" href="../Ex201_PSC_tensorGrid/">PSC device on 2D domain (Tensor grid).</a></li><li><a class="tocitem" href="../Grid_3D/">Example code for a 3D grid.</a></li><li><a class="tocitem" href="../Non_RectangularGrid_2D/">Example code for a 2D non rectangular grid.</a></li><li><a class="tocitem" href="../PSC_2D_unstructuredGrid/">PSC device on 2D domain (unstructured grid).</a></li><li><a class="tocitem" href="../PSC_3D/">Illustrative example of a three dimensional simulation.</a></li></ul></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><a class="docs-sidebar-button docs-navbar-link fa-solid fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>PSC device with surface recombination (1D).</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>PSC device with surface recombination (1D).</a></li></ul></nav><div class="docs-right"><a class="docs-navbar-link" href="https://github.com/PatricioFarrell/ChargeTransport.jl" title="Edit source on GitHub"><span class="docs-icon fa-solid"></span></a><a class="docs-settings-button docs-navbar-link fa-solid fa-gear" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-article-toggle-button fa-solid fa-chevron-up" id="documenter-article-toggle-button" href="javascript:;" title="Collapse all docstrings"></a></div></header><article class="content" id="documenter-page"><h1 id="PSC-device-with-surface-recombination-(1D)."><a class="docs-heading-anchor" href="#PSC-device-with-surface-recombination-(1D).">PSC device with surface recombination (1D).</a><a id="PSC-device-with-surface-recombination-(1D).-1"></a><a class="docs-heading-anchor-permalink" href="#PSC-device-with-surface-recombination-(1D)." title="Permalink"></a></h1><p>(<a href="https://github.com/PatricioFarrell/ChargeTransport.jl/tree/master/examples/Ex106_PSC_SurfaceRecombination.jl">source code</a>)</p><p>Simulating a three layer PSC device PCBM | MAPI | Pedot with mobile ions with a linear scan protocol.</p><p>Here, the surface recombination at internal boundaries is tested.</p><pre><code class="language-julia hljs">module Ex106_PSC_SurfaceRecombination

using ChargeTransport
using ExtendableGrids</code></pre><p>It seems to be the case that macos has problems with Pyplot</p><pre><code class="language-julia hljs">#using PyPlot</code></pre><p>write here instead of &quot;nothing&quot; Pyplot</p><pre><code class="language-julia hljs">function main(;n = 6, Plotter = nothing, plotting = false,
               verbose = false, test = false,
               parameter_file = &quot;../parameter_files/Params_PSC_PCBM_MAPI_Pedot.jl&quot;, # choose the parameter file
              )

    if plotting
        Plotter.close(&quot;all&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Define physical parameters and model&quot;)
    end
    ################################################################################

    include(parameter_file) # include the parameter file we specified

    # contact voltage
    voltageAcceptor = 1.2 * V

    # primary data for I-V scan protocol
    scanrate   = 1.0 * V/s
    ntsteps    = 31
    vend       = voltageAcceptor # bias goes until the given voltage at acceptor boundary
    tend       = vend/scanrate

    # with fixed timestep sizes we can calculate the times a priori
    tvalues    = range(0, stop = tend, length = ntsteps)

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Set up grid and regions&quot;)
    end
    ################################################################################

    δ          = 4*n        # the larger, the finer the mesh
    t          = 0.5*(cm)/δ # tolerance for geomspace and glue (with factor 10)
    k          = 1.5        # the closer to 1, the closer to the boundary geomspace

    coord_n_u  = collect(range(0.0, h_ndoping/2, step=h_ndoping/(0.8*δ)))
    coord_n_g  = geomspace(h_ndoping/2, h_ndoping,
                           h_ndoping/(0.7*δ), h_ndoping/(1.1*δ),
                           tol=t)
    coord_i_g1 = geomspace(h_ndoping, h_ndoping+h_intrinsic/k,
                           h_intrinsic/(5.1*δ), h_intrinsic/(1.1*δ),
                           tol=t)
    coord_i_g2 = geomspace(h_ndoping+h_intrinsic/k, h_ndoping+h_intrinsic,
                            h_intrinsic/(1.1*δ), h_intrinsic/(5.1*δ),
                           tol=t)
    coord_p_g  = geomspace(h_ndoping+h_intrinsic, h_ndoping+h_intrinsic+h_pdoping/2,
                           h_pdoping/(1.3*δ), h_pdoping/(0.6*δ),
                           tol=t)
    coord_p_u  = collect(range(h_ndoping+h_intrinsic+h_pdoping/2, h_ndoping+h_intrinsic+h_pdoping, step=h_pdoping/(0.8*δ)))

    coord      = glue(coord_n_u, coord_n_g,  tol=10*t)
    coord      = glue(coord,     coord_i_g1, tol=10*t)
    coord      = glue(coord,     coord_i_g2, tol=10*t)
    coord      = glue(coord,     coord_p_g,  tol=10*t)
    coord      = glue(coord,     coord_p_u,  tol=10*t)
    grid       = ExtendableGrids.simplexgrid(coord)

    # set different regions in grid
    cellmask!(grid,  [0.0 * μm],        [heightLayers[1]], regionDonor, tol = 1.0e-18)     # n-doped region   = 1
    cellmask!(grid,  [heightLayers[1]], [heightLayers[2]], regionIntrinsic, tol = 1.0e-18) # intrinsic region = 2
    cellmask!(grid,  [heightLayers[2]], [heightLayers[3]], regionAcceptor, tol = 1.0e-18)  # p-doped region   = 3

    # bfacemask! for setting different boundary regions
    bfacemask!(grid, [0.0],             [0.0],             bregionDonor, tol = 1.0e-18)     # outer left boundary
    bfacemask!(grid, [h_total],         [h_total],         bregionAcceptor, tol = 1.0e-18)  # outer right boundary
    bfacemask!(grid, [heightLayers[1]], [heightLayers[1]], bregionJ1, tol = 1.0e-18) # first  inner interface
    bfacemask!(grid, [heightLayers[2]], [heightLayers[2]], bregionJ2, tol = 1.0e-18) # second inner interface

    if plotting
        gridplot(grid, Plotter = Plotter, legend=:lt)
        Plotter.title(&quot;Grid&quot;)
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Define System and fill in information about model&quot;)
    end
    ################################################################################

    # Initialize Data instance and fill in data
    data                                = Data(grid, numberOfCarriers)

    # Possible choices: Stationary, Transient
    data.modelType                      = Transient

    # Possible choices: Boltzmann, FermiDiracOneHalfBednarczyk, FermiDiracOneHalfTeSCA,
    # FermiDiracMinusOne, Blakemore
    data.F                              = [FermiDiracOneHalfTeSCA, FermiDiracOneHalfTeSCA, FermiDiracMinusOne]

    data.bulkRecombination              = set_bulk_recombination(;iphin = iphin, iphip = iphip,
                                                                 bulk_recomb_Auger = false,
                                                                 bulk_recomb_radiative = true,
                                                                 bulk_recomb_SRH = true)

    # Possible choices: OhmicContact, SchottkyContact (outer boundary) and InterfaceNone,
    # InterfaceRecombination (inner boundary).
    data.boundaryType[bregionAcceptor]  = OhmicContact
    data.boundaryType[bregionJ1]        = InterfaceRecombination
    data.boundaryType[bregionJ2]        = InterfaceRecombination
    data.boundaryType[bregionDonor]     = OhmicContact

    # Present ionic vacancies in perovskite layer
    enable_ionic_carrier!(data, ionicCarrier = iphia, regions = [regionIntrinsic])

    # Choose flux discretization scheme: ScharfetterGummel, ScharfetterGummelGraded,
    # ExcessChemicalPotential, ExcessChemicalPotentialGraded, DiffusionEnhanced, GeneralizedSG
    data.fluxApproximation             .= ExcessChemicalPotential

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Define Params and fill in physical parameters&quot;)
    end
    ################################################################################

    params                                                = Params(grid, numberOfCarriers)

    params.temperature                                    = T
    params.UT                                             = (kB * params.temperature) / q
    params.chargeNumbers[iphin]                           = zn
    params.chargeNumbers[iphip]                           = zp
    params.chargeNumbers[iphia]                           = za

    for ireg in 1:numberOfRegions ## interior region data

        params.dielectricConstant[ireg]                   = ε[ireg] * ε0

        # effective dos, band edge energy and mobilities
        params.densityOfStates[iphin, ireg]               = Nn[ireg]
        params.densityOfStates[iphip, ireg]               = Np[ireg]
        params.densityOfStates[iphia, ireg]               = Na[ireg]

        params.bandEdgeEnergy[iphin, ireg]                = En[ireg]
        params.bandEdgeEnergy[iphip, ireg]                = Ep[ireg]
        params.bandEdgeEnergy[iphia, ireg]                = Ea[ireg]

        params.mobility[iphin, ireg]                      = μn[ireg]
        params.mobility[iphip, ireg]                      = μp[ireg]
        params.mobility[iphia, ireg]                      = μa[ireg]

        # recombination parameters
        params.recombinationRadiative[ireg]               = r0[ireg]
        params.recombinationSRHLifetime[iphin, ireg]      = τn[ireg]
        params.recombinationSRHLifetime[iphip, ireg]      = τp[ireg]
        params.recombinationSRHTrapDensity[iphin, ireg]   = trap_density!(iphin, ireg, params, EI[ireg])
        params.recombinationSRHTrapDensity[iphip, ireg]   = trap_density!(iphip, ireg, params, EI[ireg])
    end

    ##############################################################
    # inner boundary region data (we choose the intrinsic values)
    params.bDensityOfStates[iphin, bregionJ1]             = Nn[regionIntrinsic]
    params.bDensityOfStates[iphip, bregionJ1]             = Np[regionIntrinsic]

    params.bDensityOfStates[iphin, bregionJ2]             = Nn[regionIntrinsic]
    params.bDensityOfStates[iphip, bregionJ2]             = Np[regionIntrinsic]

    params.bBandEdgeEnergy[iphin, bregionJ1]              = En[regionIntrinsic]
    params.bBandEdgeEnergy[iphip, bregionJ1]              = Ep[regionIntrinsic]

    params.bBandEdgeEnergy[iphin, bregionJ2]              = En[regionIntrinsic]
    params.bBandEdgeEnergy[iphip, bregionJ2]              = Ep[regionIntrinsic]

    # for surface recombination
    params.recombinationSRHvelocity[iphin, bregionJ1]     = 1.0e1  * cm / s
    params.recombinationSRHvelocity[iphip, bregionJ1]     = 1.0e5  * cm / s

    params.bRecombinationSRHTrapDensity[iphin, bregionJ1] = params.recombinationSRHTrapDensity[iphin, regionIntrinsic]
    params.bRecombinationSRHTrapDensity[iphip, bregionJ1] = params.recombinationSRHTrapDensity[iphip, regionIntrinsic]

    params.recombinationSRHvelocity[iphin, bregionJ2]     = 1.0e7  * cm / s
    params.recombinationSRHvelocity[iphip, bregionJ2]     = 1.0e1  * cm / s

    params.bRecombinationSRHTrapDensity[iphin, bregionJ2] = params.recombinationSRHTrapDensity[iphin, regionIntrinsic]
    params.bRecombinationSRHTrapDensity[iphip, bregionJ2] = params.recombinationSRHTrapDensity[iphip, regionIntrinsic]

    ##############################################################

    # interior doping
    params.doping[iphin,  regionDonor]                    = Cn
    params.doping[iphip,  regionAcceptor]                 = Cp
    params.doping[iphia,  regionIntrinsic]                = Ca

    data.params                                           = params
    ctsys                                                 = System(grid, data, unknown_storage=:sparse)

    if test == false
        println(&quot;*** done\n&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;Define control parameters for Solver&quot;)
    end
    ################################################################################

    control              = SolverControl()
    control.verbose      = verbose
    control.damp_initial = 0.9
    control.damp_growth  = 1.61 # &gt;= 1
    control.max_round    = 5

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Compute solution in thermodynamic equilibrium&quot;)
    end
    ################################################################################

    solution = equilibrium_solve!(ctsys, control = control)
    inival   = solution

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;I-V Measurement Loop&quot;)
    end
    ################################################################################

    # for saving I-V data
    IV         = zeros(0) # for IV values
    biasValues = zeros(0) # for bias values

    for istep = 2:ntsteps

        t  = tvalues[istep]       # Actual time
        Δu = t * scanrate         # Applied voltage
        Δt = t - tvalues[istep-1] # Time step size

        # Apply new voltage (set non-equilibrium values)
        set_contact!(ctsys, bregionAcceptor, Δu = Δu)

        if test == false
            println(&quot;time value: Δt = $(t)&quot;)
        end

        solution = solve(ctsys, inival = inival, control = control, tstep = Δt)
        inival   = solution

        # get I-V data
        current  = get_current_val(ctsys, solution, inival, Δt)

        push!(IV, current)
        push!(biasValues, Δu)

        if plotting
            label_solution, label_density, label_energy = set_plotting_labels(data)
            label_solution[iphia]  = &quot;\$ \\varphi_a\$&quot;

            Plotter.clf()
            plot_solution(Plotter, ctsys, solution, &quot;bias \$\\Delta u\$ = $(Δu)&quot;, label_solution)
            Plotter.pause(0.5)
        end

    end # time loop

    ##res = [biasValues, IV]

    if test == false
        println(&quot;*** done\n&quot;)
    end

    testval = sum(filter(!isnan, solution))/length(solution) # when using sparse storage, we get NaN values in solution
    return testval


end # main

function test()
    testval = -0.5963272869004673
    main(test = true) ≈ testval
end

if test == false
    println(&quot;This message should show when this module is successfully recompiled.&quot;)
end

end # module</code></pre><hr/><p><em>This page was generated using <a href="https://github.com/fredrikekre/Literate.jl">Literate.jl</a>.</em></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../Ex105_PSC_gradedFlux/">« Three-layer PSC device with graded interfaces &amp; Ohmic contacts (1D).</a><a class="docs-footer-nextpage" href="../Ex107_MoS2_withIons_BarrierLowering/">MoS2 with moving defects and Schottky Barrier Lowering. »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="auto">Automatic (OS)</option><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 1.4.0 on <span class="colophon-date" title="Monday 22 April 2024 06:44">Monday 22 April 2024</span>. Using Julia version 1.10.2.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>

<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>PSC device with Beer-Lambert generation rate (1D). · ChargeTransport.jl</title><script data-outdated-warner src="../../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.045/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.13.24/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../../general/">ChargeTransport.jl</a></span></div><form class="docs-search" action="../../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../../general/">ChargeTransport.jl – Simulating charge transport in semiconductors</a></li><li><a class="tocitem" href="../../backgroundinfo/">Mathematical drift-diffusion models</a></li><li><span class="tocitem">How to get started</span><ul><li><a class="tocitem" href="../../GeneralInformation/">Code overview</a></li><li><a class="tocitem" href="../../GaAs/">van Roosbroeck system</a></li><li><a class="tocitem" href="../../PSC/">Perovskite solar cell</a></li></ul></li><li><a class="tocitem" href="../../allindex/">Types, Constructors and Methods</a></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../Ex101_PIN/">GaAs diode (1D).</a></li><li><a class="tocitem" href="../Ex102_PIN_nodal_doping/">GaAs diode with spatially varying doping (1D).</a></li><li><a class="tocitem" href="../Ex103_PSC/">PSC device without mobile ions (1D).</a></li><li><a class="tocitem" href="../Ex104_PSC_gradedFlux_Schottky_contacts/">PSC device with graded interfaces &amp; Schottky contacts (1D).</a></li><li><a class="tocitem" href="../Ex105_PSC_gradedFlux/">PSC device with graded interfaces &amp; Ohmic contacts (1D).</a></li><li><a class="tocitem" href="../Ex106_PSC_withIons_IVMeasurement/">PSC device with ions and linear I-V scan protocol (1D).</a></li><li><a class="tocitem" href="../Ex107_PSC_withIons_sinusoidalVoltage/">PSC device with sinusoidal applied voltage (1D).</a></li><li class="is-active"><a class="tocitem" href>PSC device with Beer-Lambert generation rate (1D).</a></li><li><a class="tocitem" href="../Ex108_PSC_uniform_Generation/">PSC device with uniform generation rate (1D).</a></li><li><a class="tocitem" href="../Ex109_Traps/">GaAs diode: transient with traps (1D).</a></li><li><a class="tocitem" href="../Ex110_PSC_surface_recombination/">PSC device with surface recombination (1D).</a></li><li><a class="tocitem" href="../Ex111_MoS2_withIons_BarrierLowering/">-</a></li><li><a class="tocitem" href="../Ex112_CIGS_WithTraps/">CIGS pn junction: stationary with traps and Schottky contacts.</a></li><li><a class="tocitem" href="../Ex201_PSC_tensorGrid/">PSC device on 2D domain (Tensor grid).</a></li><li><a class="tocitem" href="../Grid_3D/">Example code for a 3D grid.</a></li><li><a class="tocitem" href="../Non_RectangularGrid_2D/">Example code for a 2D non rectangular grid.</a></li><li><a class="tocitem" href="../PSC_2D_unstructuredGrid/">PSC device on 2D domain (unstructured grid).</a></li></ul></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>PSC device with Beer-Lambert generation rate (1D).</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>PSC device with Beer-Lambert generation rate (1D).</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/PatricioFarrell/ChargeTransport.jl" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="PSC-device-with-Beer-Lambert-generation-rate-(1D)."><a class="docs-heading-anchor" href="#PSC-device-with-Beer-Lambert-generation-rate-(1D).">PSC device with Beer-Lambert generation rate (1D).</a><a id="PSC-device-with-Beer-Lambert-generation-rate-(1D).-1"></a><a class="docs-heading-anchor-permalink" href="#PSC-device-with-Beer-Lambert-generation-rate-(1D)." title="Permalink"></a></h1><p>(<a href="https://github.com/PatricioFarrell/ChargeTransport.jl/tree/master/examplesEx108_PSC_BeerLambert_Generation.jl">source code</a>)</p><p>Simulating a three layer PSC device SiO2| MAPI | SiO2 with mobile ions where the ion vacancy accumulation is limited by the Fermi-Dirac integral of order -1. The simulations are performed out of equilibrium, time-dependent and with abrupt interfaces. A linear I-V measurement protocol is included and the corresponding solution vectors after the scan can be depicted.</p><p>The parameters are the default parameters in IonMonger.</p><pre><code class="language-julia hljs">module Ex108_PSC_BeerLambert_Generation

using ChargeTransport
using ExtendableGrids
using PyPlot

function main(;n = 8, Plotter = PyPlot, plotting = false, verbose = false, test = false)

    ################################################################################
    if test == false
        println(&quot;Set up grid and regions&quot;)
    end
    ################################################################################
    # region numbers
    regionDonor      = 1                           # n doped region
    regionIntrinsic  = 2                           # intrinsic region
    regionAcceptor   = 3                           # p doped region
    regions          = [regionDonor, regionIntrinsic, regionAcceptor]
    numberOfRegions  = length(regions)

    # boundary region numbers
    bregionDonor     = 1
    bregionAcceptor  = 2
    bregionJunction1 = 3
    bregionJunction2 = 4

    # grid
    h_ndoping        = 1.00e-5 * cm
    h_intrinsic      = 4.00e-5 * cm
    h_pdoping        = 2.00e-5 * cm
    heightLayers     = [h_ndoping,
                        h_ndoping + h_intrinsic,
                        h_ndoping + h_intrinsic + h_pdoping]

    x0               = 0.0 * cm
    δ                = 4*n        # the larger, the finer the mesh
    t                = 0.5*(cm)/δ # tolerance for geomspace and glue (with factor 10)
    k                = 1.5        # the closer to 1, the closer to the boundary geomspace

    coord_n_u        = collect(range(x0, h_ndoping/2, step=h_ndoping/(0.8*δ)))
    coord_n_g        = geomspace(h_ndoping/2,
                                 h_ndoping,
                                 h_ndoping/(0.7*δ),
                                 h_ndoping/(1.1*δ),
                                 tol=t)
    coord_i_g1       = geomspace(h_ndoping,
                                 h_ndoping+h_intrinsic/k,
                                 h_intrinsic/(2.8*δ),
                                 h_intrinsic/(2.1*δ),
                                 tol=t)
    coord_i_g2       = geomspace(h_ndoping+h_intrinsic/k,
                                 h_ndoping+h_intrinsic,
                                 h_intrinsic/(2.1*δ),
                                 h_intrinsic/(2.8*δ),
                                 tol=t)
    coord_p_g        = geomspace(h_ndoping+h_intrinsic,
                                 h_ndoping+h_intrinsic+h_pdoping/2,
                                 h_pdoping/(1.6*δ),
                                 h_pdoping/(1.6*δ),
                                 tol=t)
    coord_p_u        = collect(range(h_ndoping+h_intrinsic+h_pdoping/2, h_ndoping+h_intrinsic+h_pdoping, step=h_pdoping/(1.3*δ)))

    coord            = glue(coord_n_u, coord_n_g,  tol=10*t)
    coord            = glue(coord,     coord_i_g1, tol=10*t)
    coord            = glue(coord,     coord_i_g2, tol=10*t)
    coord            = glue(coord,     coord_p_g,  tol=10*t)
    coord            = glue(coord,     coord_p_u,  tol=10*t)
    grid             = ExtendableGrids.simplexgrid(coord)

    # set different regions in grid, doping profiles do not intersect
    cellmask!(grid, [0.0 * μm],        [heightLayers[1]], regionDonor, tol = 1.0e-18)     # n-doped region   = 1
    cellmask!(grid, [heightLayers[1]], [heightLayers[2]], regionIntrinsic, tol = 1.0e-18) # intrinsic region = 2
    cellmask!(grid, [heightLayers[2]], [heightLayers[3]], regionAcceptor, tol = 1.0e-18)  # p-doped region   = 3

    bfacemask!(grid, [heightLayers[1]], [heightLayers[1]], bregionJunction1, tol = 1.0e-18)
    bfacemask!(grid, [heightLayers[2]], [heightLayers[2]], bregionJunction2, tol = 1.0e-18)

    if plotting
        gridplot(grid, Plotter = Plotter)
        Plotter.title(&quot;Grid&quot;)
        Plotter.figure()
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;Define physical parameters and model&quot;)
    end
    ################################################################################

    # set indices of the quasi Fermi potentials
    iphin              = 1 # electron quasi Fermi potential
    iphip              = 2 # hole quasi Fermi potential
    iphia              = 3 # anion vacancy quasi Fermi potential

    numberOfCarriers   = 3 # electrons, holes and anion vacancies

    # temperature
    T                  = 298.0                 *  K

    # band edge energies
    Ec_d               = -4.0                  *  eV
    Ev_d               = -5.8                  *  eV

    Ec_i               = -3.7                  *  eV
    Ev_i               = -5.4                  *  eV

    Ec_a               = -3.4                  *  eV
    Ev_a               = -5.1                  *  eV

    # ############ adjust Na, Ea for anion vacancies here ###########
    Nanion             = 1.0e21                / (cm^3)
    Ea_i               = -4.45                 *  eV
    # ############ adjust Na, Ea for anion vacancies here ###########

    EC                 = [Ec_d, Ec_i, Ec_a]
    EV                 = [Ev_d, Ev_i, Ev_a]
    EA                 = [0.0,  Ea_i,  0.0]

    # effective densities of state
    Nc_d               = 5.0e19                / (cm^3)
    Nv_d               = 5.0e19                / (cm^3)

    Nc_i               = 8.1e18                / (cm^3)
    Nv_i               = 5.8e18                / (cm^3)

    Nc_a               = 5.0e19                / (cm^3)
    Nv_a               = 5.0e19                / (cm^3)

    NC                 = [Nc_d, Nc_i,  Nc_a]
    NV                 = [Nv_d, Nv_i,  Nv_a]
    NAnion             = [0.0,  Nanion, 0.0]

    # mobilities
    μn_d             = 3.89                    * (cm^2) / (V * s)
    μp_d             = 3.89                    * (cm^2) / (V * s)

    μn_i             = 6.62e1                  * (cm^2) / (V * s)
    μp_i             = 6.62e1                  * (cm^2) / (V * s)

    μa_i             = 3.93e-12                * (cm^2) / (V * s)

    μn_a             = 3.89e-1                 * (cm^2) / (V * s)
    μp_a             = 3.89e-1                 * (cm^2) / (V * s)

    μn               = [μn_d, μn_i, μn_a]
    μp               = [μp_d, μp_i, μp_a]
    μa               = [0.0,  μa_i, 0.0 ]

    # relative dielectric permittivity
    ε_d                = 10.0                  *  1.0
    ε_i                = 24.1                  *  1.0
    ε_a                = 3.0                   *  1.0

    ε                  = [ε_d, ε_i, ε_a]

    # radiative recombination
    r0_d               = 0.0e0                 * cm^3 / s
    r0_i               = 0.0e0                 * cm^3 / s
    r0_a               = 0.0e0                 * cm^3 / s

    r0                 = [r0_d, r0_i, r0_a]

    # life times and trap densities
    τn_d               = 1.0e100               * s
    τp_d               = 1.0e100               * s

    τn_i               = 3.0e-9                * s
    τp_i               = 3.0e-7                * s
    τn_a               = τn_d
    τp_a               = τp_d
    τn                 = [τn_d, τn_i, τn_a]
    τp                 = [τp_d, τp_i, τp_a]

    # SRH trap energies
    Ei_d               = -5.0                  * eV
    Ei_i               = -4.55                 * eV
    Ei_a               = -4.1                  * eV

    EI                 = [Ei_d, Ei_i, Ei_a]

    # Auger recombination
    Auger              = 0.0

    # generation
    photonflux_i       = 8.0e20                / (m^2 * s)
    absorption_i       = 1.3e7                 / m
    incidentPhotonFlux = [0.0, photonflux_i, 0.0]
    absorption         = [0.0, absorption_i, 0.0]
    generationPeak     = h_ndoping

    # doping
    Nd                 = 1.00e18               / (cm^3)
    Na                 = 1.00e18               / (cm^3)
    C0                 = 1.6e19                / (cm^3)

    # contact voltage
    voltageAcceptor    = 1.2                   * V

    # primary data for I-V scan protocol
    scanrate           = 0.4 * V/s
    number_tsteps      = 101
    endVoltage         = voltageAcceptor # bias goes until the given voltage at acceptor boundary
    tend               = endVoltage/scanrate

    # Define scan protocol function
    function scanProtocol(t)

        if    0.0 &lt;= t  &amp;&amp; t &lt;= tend
            biasVal = 0.0 + scanrate * t
        elseif  t &gt; tend  &amp;&amp; t &lt;= 2*tend
            biasVal = scanrate * tend .+ scanrate * (tend - t)
        else
            biasVal = 0.0
        end

        return biasVal

    end

    contactVoltageFunction = [zeroVoltage, scanProtocol]

    if test == false
        println(&quot;*** done\n&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;Define System and fill in information about model&quot;)
    end
    ################################################################################

    # Initialize Data instance and fill in predefined data
    data                               = Data(grid, numberOfCarriers, contactVoltageFunction = contactVoltageFunction)
    data.modelType                     = Transient
    data.F                             = [Boltzmann, Boltzmann, FermiDiracMinusOne]

    data.bulkRecombination             = set_bulk_recombination(;iphin = iphin, iphip = iphip,
                                                                 bulk_recomb_Auger = true,
                                                                 bulk_recomb_radiative = true,
                                                                 bulk_recomb_SRH = true)
    data.boundaryType[bregionAcceptor] = OhmicContact
    data.boundaryType[bregionDonor]    = OhmicContact
    data.generationModel               = GenerationBeerLambert
    data.fluxApproximation            .= ExcessChemicalPotential

    enable_ionic_carrier!(data, ionicCarrier = iphia, regions = [regionIntrinsic])

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Define Params and fill in physical parameters&quot;)
    end
    ################################################################################

    params                                              = Params(grid, numberOfCarriers)

    params.temperature                                  = T
    params.UT                                           = (kB * params.temperature) / q
    params.chargeNumbers[iphin]                         = -1
    params.chargeNumbers[iphip]                         =  1
    params.chargeNumbers[iphia]                         =  1

    for ireg in 1:numberOfRegions # interior region data

        params.dielectricConstant[ireg]                 = ε[ireg] * ε0

        # effective DOS, band edge energy and mobilities
        params.densityOfStates[iphin, ireg]             = NC[ireg]
        params.densityOfStates[iphip, ireg]             = NV[ireg]
        params.densityOfStates[iphia, ireg]             = NAnion[ireg]

        params.bandEdgeEnergy[iphin, ireg]              = EC[ireg]
        params.bandEdgeEnergy[iphip, ireg]              = EV[ireg]
        params.bandEdgeEnergy[iphia, ireg]              = EA[ireg]

        params.mobility[iphin, ireg]                    = μn[ireg]
        params.mobility[iphip, ireg]                    = μp[ireg]
        params.mobility[iphia, ireg]                    = μa[ireg]

        # recombination parameters
        params.recombinationRadiative[ireg]             = r0[ireg]
        params.recombinationSRHLifetime[iphin, ireg]    = τn[ireg]
        params.recombinationSRHLifetime[iphip, ireg]    = τp[ireg]
        params.recombinationSRHTrapDensity[iphin, ireg] = trap_density!(iphin, ireg, data, EI[ireg])
        params.recombinationSRHTrapDensity[iphip, ireg] = trap_density!(iphip, ireg, data, EI[ireg])
        params.recombinationAuger[iphin, ireg]          = Auger
        params.recombinationAuger[iphip, ireg]          = Auger

        # generation parameters
        params.generationIncidentPhotonFlux[ireg]       = incidentPhotonFlux[ireg]
        params.generationAbsorption[ireg]               = absorption[ireg]
    end</code></pre><p>parameter which passes the shift information in the Beer-Lambert generation</p><pre><code class="language-julia hljs">    params.generationPeak                               = generationPeak

    # boundary region data
    params.bDensityOfStates[iphin, bregionDonor]        = Nc_d
    params.bDensityOfStates[iphip, bregionDonor]        = Nv_d

    params.bDensityOfStates[iphin, bregionAcceptor]     = Nc_a
    params.bDensityOfStates[iphip, bregionAcceptor]     = Nv_a

    params.bBandEdgeEnergy[iphin, bregionDonor]         = Ec_d
    params.bBandEdgeEnergy[iphip, bregionDonor]         = Ev_d

    params.bBandEdgeEnergy[iphin, bregionAcceptor]      = Ec_a
    params.bBandEdgeEnergy[iphip, bregionAcceptor]      = Ev_a

    # interior doping
    params.doping[iphin, regionDonor]                   = Nd
    params.doping[iphia, regionIntrinsic]               = C0
    params.doping[iphip, regionAcceptor]                = Na

    # boundary doping
    params.bDoping[iphip, bregionAcceptor]              = Na
    params.bDoping[iphin, bregionDonor]                 = Nd

    data.params                                         = params
    ctsys                                               = System(grid, data, unknown_storage=:sparse)

    if test == false
        println(&quot;*** done\n&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;Define control parameters for Newton solver&quot;)
    end
    ################################################################################

    control                   = NewtonControl()
    control.verbose           = verbose
    control.max_iterations    = 300
    control.tol_absolute      = 1.0e-10
    control.tol_relative      = 1.0e-10
    control.handle_exceptions = true
    control.tol_round         = 1.0e-10
    control.max_round         = 5
    control.damp_initial      = 0.5
    control.damp_growth       = 1.21 # &gt;= 1
    control.Δt_max            = 5.0e-2
    if test == false
        control.print_time    = true
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;Compute solution in thermodynamic equilibrium&quot;)
    end
    ################################################################################

    # initialize solution and starting vectors
    initialGuess  = unknowns(ctsys)
    solution      = unknowns(ctsys)

    solution      = equilibrium_solve!(ctsys, control = control, nonlinear_steps = 20)

    initialGuess .= solution

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Loop for generation&quot;)
    end
    ################################################################################

    ctsys.data.calculationType = OutOfEquilibrium
    ctsys.data.λ2              = 0.0</code></pre><p>these values are needed for putting the generation slightly on</p><pre><code class="language-julia hljs">    I      = collect(20:-1:0.0)
    LAMBDA = 10 .^ (-I)

    # since the constant which represents the constant quasi Fermi potential of anion vacancies is undetermined, we need
    # to fix it in the bias loop, since we have no applied bias. Otherwise we get convergence errors
    ctsys.fvmsys.boundary_factors[iphia, bregionJunction2] = 1.0e30
    ctsys.fvmsys.boundary_values[iphia, bregionJunction2]  = 0.0

    for istep = 1:length(I)-1

        # turn slowly generation on
        ctsys.data.λ2   = LAMBDA[istep + 1]

        if test == false
            println(&quot;increase generation with λ2 = $(data.λ2)&quot;)
        end

        solve!(solution, initialGuess, ctsys, control  = control, tstep = Inf)

        initialGuess .= solution

    end # generation loop

    if plotting
        label_solution, label_density, label_energy, label_BEE = set_plotting_labels(data)

        # add labels for anion vacancy
        label_energy[1, iphia] = &quot;\$E_a-q\\psi\$&quot;; label_energy[2, iphia] = &quot;\$ - q \\varphi_a\$&quot;; label_BEE[iphia] = &quot;\$E_a\$&quot;
        label_density[iphia]   = &quot;a&quot;;              label_solution[iphia]  = &quot;\$ \\varphi_a\$&quot;

        plot_densities(Plotter, ctsys, solution, &quot;Initial condition&quot;, label_density)
        Plotter.legend()
        Plotter.figure()
        plot_solution(Plotter, ctsys, solution, &quot;Initial condition&quot;, label_solution)
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;IV Measurement loop&quot;)
    end
    ################################################################################

    # put here back the homogenous Neumann boundary conditions.
    ctsys.fvmsys.boundary_factors[iphia, bregionJunction2] = 0.0
    ctsys.fvmsys.boundary_values[iphia, bregionJunction2]  = 0.0

    sol = solve(ctsys, inival = initialGuess, times=(0.0, tend), control = control)

    if plotting

        tsol = sol(tend)
        plot_densities(Plotter, ctsys, tsol, &quot;Densities at end time&quot;, label_density)
        Plotter.legend()
        Plotter.figure()
        plot_solution(Plotter, ctsys, tsol, &quot;Solution at end time&quot;, label_solution)
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Reverse scan protocol&quot;)
    end
    ################################################################################

    initialGuessReverse = sol(tend)

    solReverse = solve(ctsys, inival = initialGuessReverse, times=(tend, 2 * tend), control = control)

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;IV Curve calculation&quot;)
    end
    ################################################################################

    factory       = TestFunctionFactory(ctsys)
    tf            = testfunction(factory, [bregionDonor], [bregionAcceptor])

    tvalues       = sol.t
    number_tsteps = length(tvalues)
    biasValues    = scanProtocol.(tvalues)
    IV            = zeros(0)

    for istep = 2:number_tsteps
        Δt       = tvalues[istep] - tvalues[istep-1] # Time step size
        inival   = sol[istep-1]
        solution = sol[istep]

        I        = integrate(ctsys, tf, solution, inival, Δt)

        current = 0.0
        for ii = 1:numberOfCarriers+1
            current = current + I[ii]
        end

        push!(IV, current)

    end

    tvaluesReverse       = solReverse.t
    number_tstepsReverse = length(tvaluesReverse)
    biasValuesReverse    = scanProtocol.(tvaluesReverse)
    IVReverse            = zeros(0)

    for istep = 2:number_tstepsReverse
        Δt       = tvaluesReverse[istep] - tvaluesReverse[istep-1] # Time step size
        inival   = solReverse[istep-1]
        solution = solReverse[istep]

        I        = integrate(ctsys, tf, solution, inival, Δt)

        current = 0.0
        for ii = 1:numberOfCarriers+1
            current = current + I[ii]
        end

        push!(IVReverse, current)

    end

    if plotting
        Plotter.figure()
        Plotter.plot([tvalues tvaluesReverse], [biasValues biasValuesReverse], marker = &quot;x&quot;)
        Plotter.xlabel(&quot;time [s]&quot;)
        Plotter.ylabel(&quot;voltage [V]&quot;)
        Plotter.grid()

        Plotter.figure()
        Plotter.plot(biasValues[2:end], -IV, linewidth = 5, label = &quot;forward&quot;)
        Plotter.plot(biasValuesReverse[2:end], -IVReverse, linewidth = 5, label = &quot;reverse&quot;)
        Plotter.grid()
        PyPlot.legend()
        Plotter.xlabel(&quot;applied bias [V]&quot;)
        Plotter.ylabel(&quot;total current [A]&quot;)
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Compute fill factor and efficiency&quot;)
    end
    ################################################################################

    bias                      = biasValues[2:end]
    IV                        = -IV

    powerDensity              = bias .* (IV)           # power density function
    MaxPD, indexPD            = findmax(powerDensity)

    open_circuit              = compute_open_circuit_voltage(bias, IV)

    IncidentLightPowerDensity = 1000.0 * W/m^2

    efficiency                =  bias[indexPD] * IV[indexPD]  / IncidentLightPowerDensity
    fillfactor                = (bias[indexPD] * IV[indexPD]) / (IV[1] * open_circuit)

    if test == false
        println(&quot;The fill factor is $fillfactor.&quot;)
        println(&quot;The efficiency  is $efficiency%.&quot;)
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    testval = sum(filter(!isnan, solution))/length(solution) # when using sparse storage, we get NaN values in solution
    return testval

end #  main

function test()
    testval = -1.0976590711344825
    main(test = true) ≈ testval
end

if test == false
    println(&quot;This message should show when this module is successfully recompiled.&quot;)
end

end # module</code></pre><hr/><p><em>This page was generated using <a href="https://github.com/fredrikekre/Literate.jl">Literate.jl</a>.</em></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../Ex107_PSC_withIons_sinusoidalVoltage/">« PSC device with sinusoidal applied voltage (1D).</a><a class="docs-footer-nextpage" href="../Ex108_PSC_uniform_Generation/">PSC device with uniform generation rate (1D). »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 0.27.24 on <span class="colophon-date" title="Wednesday 1 February 2023 14:52">Wednesday 1 February 2023</span>. Using Julia version 1.8.5.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
